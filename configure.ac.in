dnl configure.ac
dnl
dnl xfce4-netload-plugin - Netload plugin for xfce4-panel
dnl
dnl Copyright 2003 Benedikt Meurer <benedikt.meurer@unix-ag.uni-siegen.de>
dnl Copyright 2010 Florian Rivoal <frivoal@xfce.org>
dnl Copyright 2014 Mike Massonnet <mmassonnet@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([netload_version_major], [1])
m4_define([netload_version_minor], [4])
m4_define([netload_version_micro], [0])
m4_define([netload_version_build], [@REVISION@])
m4_define([netload_version_tag], [git])
m4_define([netload_version], [netload_version_major().netload_version_minor().netload_version_micro()ifelse(netload_version_tag(), [git], [netload_version_tag()-netload_version_build()], [netload_version_tag()])])

m4_define([netload_debug_default], [ifelse(netload_version_tag(), [git], [yes], [minimum])])

AC_INIT([xfce4-netload-plugin], [netload_version],
	[xfce-dev@xfce.org])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIRS([m4])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()

AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2 foreign])
AM_MAINTAINER_MODE
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl Check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
IT_PROG_INTLTOOL([0.35.0])

dnl Substitute in Makefiles
AC_SUBST(OS)

AC_CHECK_FUNCS([gethostname])
AC_CHECK_FUNCS([memset])
AC_CHECK_FUNCS([socket])
AC_CHECK_FUNCS([strstr])
AC_CHECK_FUNCS([malloc])
AC_CHECK_FUNCS([bzero])
AC_CHECK_HEADERS([arpa/inet.h])
AC_CHECK_HEADERS([netinet/in.h])
AC_CHECK_HEADERS([stdlib.h])
AC_CHECK_HEADERS([string.h])
AC_CHECK_HEADERS([sys/ioctl.h])
AC_CHECK_HEADERS([sys/param.h])
AC_CHECK_HEADERS([sys/socket.h])
AC_CHECK_HEADERS([sys/time.h])
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([sys/sockio.h])
AC_HEADER_SYS_WAIT
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIZE_T

AC_CHECK_LIB(kstat, kstat_open, SOLLIBS="-lkstat -lsocket", SOLLIBS="")
AC_CHECK_LIB(nsl, kstat_open, SOLLIBS="$SOLLIBS -linet_ntop", SOLLIBS="$SOLLIBS")
AC_SUBST(SOLLIBS)

dnl configure the panel plugin
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])

dnl configure the libxfcegui4
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_CONFIG_FILES([
Makefile
panel-plugin/Makefile
po/Makefile.in
icons/Makefile
icons/16x16/Makefile
icons/32x32/Makefile
icons/128x128/Makefile
icons/scalable/Makefile
])
AC_OUTPUT
